;************************************************************************
;Программа 4.2 для МК ATx8515: демонстрация работы таймера Т1
;в режиме таймера. При частоте работы микроконтроллера FCK =3,69 МГц
;после нажатия SW0 на вход счётчика поступают сигналы с частотой FCK/1024,
;после нажатия SW1 - FCK /256.
;Соединения: PD0,PD1–SW0,SW1; PB-LED
;************************************************************************
;.include "8515def.inc" ;файл определений AT90S8515
.include "m8515def.inc" ;файл определений ATmega8515
.def temp = r16 ;временный регистр
.equ SW0 = 0 ;0-ой вывод порта PD
.equ SW1 = 1 ;1-ий вывод порта PD
;***Таблица векторов прерываний
.org $000
	rjmp INIT ;обработка сброса
.org $006
	rjmp T1_OVF ;обработка переполнения таймера T0
;***Инициализация МК
INIT: ldi temp,low(RAMEND) ;установка
	out SPL,temp ; указателя стека
	ldi temp,high(RAMEND) ; на последнюю
	out SPH,temp ; ячейку ОЗУ
	clr temp ;инициализация выводов порта PD
	out DDRD,temp ; на ввод
	ldi temp,0x03 ;включение ‘подтягивающих’ резисторов
	out PORTD,temp ; в разрядах 0,1 порта PD
	ser temp ;инициализация выводов порта PB
	out DDRB,temp ; на вывод
	out PORTB,temp ;выключение светодиодов
;***Настройка таймера Т1 на режим таймера
	ldi temp,0x80 ;разрешение прерывания по
	out TIMSK,temp ; переполнению таймера Т1
	clr temp ;таймер Т1
	out TCCR1B,temp ; остановлен
	ldi temp,0x80 ; загрузка TCNT1
	out TCNT1H,temp
	ldi temp,0x00
	out TCNT1L,temp
	sei ;глобальное разрешение прерываний
;***Ожидание нажатия кнопок
test_sw0: sbic PIND,SW0 ;проверка нажатия
	rjmp test_sw1 ; кнопки SW0
;***Обработка нажатия кнопки SW0
	ldi temp,0x05 ;для настройки предделителя (К=1024)
	rcall LED_ON ;включение светодиодов
test_sw1: sbic PIND,SW1 ;проверка нажатия
	rjmp test_sw0 ; кнопки SW1
;***Обработка нажатия кнопки SW1
	ldi temp,0x04 ; для настройки предделителя (К=256)
	rcall LED_ON ;включение светодиодов
	rjmp test_sw0
;***Включение светодиодов
LED_ON: out TCCR1B,temp ;запуск таймера с предделителем
	clr temp ;включение
	out PORTB,temp ; светодиодов
	ret
;***Обработка прерывания при переполнении таймера T1
T1_OVF: ser temp
	out PORTB,temp ;выключение светодиодов
	clr temp ;останов
	out TCCR1B,temp ; таймера Т1
	ldi temp,0x80
	out TCNT1H,temp ; перезагрузка TCNT1
	ldi temp,0x00
	out TCNT1L,temp
	reti
